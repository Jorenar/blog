---

layout: post
title:  "Make Vim follow XDG Base Directory specification"
categories: configuring
tags: [ xdg, vim ]

---

XDG Base Directory specification, `$XDG_CONFIG_HOME` etc. Great thing - configs
separated from user data and cache, no clutter in home directory. Unfortunately,
many programs still don't respect it, including Vim. But what would be our favourite
text editor if we wouldn't be able to reconfigure it!

# TL;DR

Into shell config (e.g. in `~/.profile`):
```sh
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export VIMINIT='if !has("nvim") | source $XDG_CONFIG_HOME/vim/xdg.vim | endif'
```

Create file `$XDG_CONFIG_HOME/vim/xdg.vim`:
```vim
" XDG support for Vim

if empty($XDG_CACHE_HOME)  | let $XDG_CACHE_HOME  = $HOME."/.cache"       | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = $HOME."/.config"      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = $HOME."/.local/share" | endif

set runtimepath^=$XDG_CONFIG_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after
set runtimepath+=$XDG_DATA_HOME/vim

set packpath^=$XDG_DATA_HOME/vim
call mkdir($XDG_DATA_HOME."/vim/spell", 'p', 0700)

set backupdir=$XDG_CACHE_HOME/vim/backup | call mkdir(&backupdir, 'p', 0700)
set directory=$XDG_CACHE_HOME/vim/swap   | call mkdir(&directory, 'p', 0700)
set undodir=$XDG_CACHE_HOME/vim/undo     | call mkdir(&undodir,   'p', 0700)
set viewdir=$XDG_CACHE_HOME/vim/view     | call mkdir(&viewdir,   'p', 0700)

if !has('nvim') " Neovim has its own special location
  set viminfofile=$XDG_CACHE_HOME/vim/viminfo
endif

if empty($MYVIMRC)
  let $MYVIMRC = $XDG_CONFIG_HOME . "/vim/vimrc"
endif

source $MYVIMRC
```

# Step-by-step

If you haven't already set `XDG_CONFIG_HOME`, then you need to at least define
in your shell config:
```sh
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
```

To begin with, since version 7.3.1178 Vim will search for `~/.vim/vimrc` if
`~/.vimrc` is not found. So let's move the file there. Yay, one less.

Let's move our `~/.vim` to `$XDG_CONFIG_HOME/vim`. Now create there\* new file
named `xdg.vim` - we will put our tweaks into it.

We need to command Vim to read this new file. Fortunately for us, there is
`VIMINIT` environmental variable\*\*.

```sh
export VIMINIT='source $XDG_CONFIG_HOME/vim/xdg.vim'
```

If you wish for Neovim and Vim configurations to still be separated, then:

```sh
export VIMINIT='if !has("nvim") | source $XDG_CONFIG_HOME/vim/xdg.vim | endif'
```

#### Now the code in our `xdg.vim`

First of all, let's define fallback locations in case `XDG_*` variables are not set.
```vim
if empty($XDG_CACHE_HOME)  | let $XDG_CACHE_HOME  = $HOME."/.cache"       | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = $HOME."/.config"      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = $HOME."/.local/share" | endif
```

Now let's add entries to `runtimepath`:
```vim
set runtimepath^=$XDG_CONFIG_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after
set runtimepath+=$XDG_DATA_HOME/vim
```

`$XDG_CONFIG_HOME/vim` and `$XDG_CONFIG_HOME/vim/after` are just equivalents of
`~/.vim` and `~/.vim/after`, but `$XDG_DATA_HOME/vim` is brand new - there we
will keep downloadables, like plugins and spell files.

Let's set directory for Vim8 build-in packages:
```vim
set packpath^=$XDG_DATA_HOME/vim
```

What about spellings? Well, this one is more tricky, because it isn't controlled
by option. Instead it searches for `spell` directory in whole runtime path. If
none is found then it falls back to `~/.vim/spell`. So let's create one at desired
location ourselves!
```vim
call mkdir($XDG_DATA_HOME."/vim/spell", 'p', 0700)
```

So far so good. We are left with cache (backup, undo, swap, view, viminfo).
Vim doesn't create directories for them (even for defaults), so we will need
to do it ourselves - thankfully VimL has `mkdir()` function.
```vim
set backupdir=$XDG_CACHE_HOME/vim/backup | call mkdir(&backupdir, 'p', 0700)
set directory=$XDG_CACHE_HOME/vim/swap   | call mkdir(&directory, 'p', 0700)
set undodir=$XDG_CACHE_HOME/vim/undo     | call mkdir(&undodir,   'p', 0700)
set viewdir=$XDG_CACHE_HOME/vim/view     | call mkdir(&viewdir,   'p', 0700)

if !has('nvim') " Neovim has its own location which already complies with XDG specification
  set viminfofile=$XDG_CACHE_HOME/vim/viminfo
endif
```

And finishing touch - sourcing `vimrc`:
```vim
if empty($MYVIMRC)
  let $MYVIMRC = $XDG_CONFIG_HOME . "/vim/vimrc"
endif

source $MYVIMRC
```

Congratulations! Now your Vim is configured with accordance to XDG Base Directory specification.

---

\* It can be wherever you want, of course. Just be sure it's sourced  \
\*\* Alternative: `alias vim='vim -u $XDG_CONFIG_HOME/vim/xdg.vim'`

# Sources
* [Vim respect XDG](https://tlvince.com/vim-respect-xdg)
* [Issue #2034: [RFC] .config/vim(UNIX), AppData/Roaming/vim(Windows) directory structure](https://github.com/vim/vim/issues/2034)
* [Version 7.3.1178 commit](https://github.com/vim/vim/commit/6a459902592e2a4ba68)
* [XDG Base Directory - ArchWiki](https://wiki.archlinux.org/index.php/XDG_Base_Directory)
* [Jorengarenar's Vim config](https://github.com/Jorengarenar/dotfiles/tree/master/vim)
