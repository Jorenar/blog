---

layout: post
title:  "Make Vim obey XDG Base Directory specification"
categories: configuring
tags: [ xdg, vim ]

---

XDG Base Directory specification, `$XDG_CONFIG_HOME` etc. Great thing - configs
separated from user data and cache, no clutter in home directory. Unfortunately,
many programs still don't respect it, including Vim. But what would be our favourite
text editor if we wouldn't be able to reconfigure it!

# TL;DR

Into shell config (e.g. in `~/.profile`):
```sh
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export VIMINIT='if !has("nvim") | set rtp^=$XDG_CONFIG_HOME/vim | let $MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc" | so $MYVIMRC | endif'
```

Somewhere near the top of _vimrc_:
```vim
" XDG support    (`set rtp^=$XDG_CONFIG_HOME/vim` is in $VIMINIT) {{{

if empty($XDG_CACHE_HOME)  | let $XDG_CACHE_HOME  = $HOME."/.cache"       | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = $HOME."/.config"      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = $HOME."/.local/share" | endif

set backupdir=$XDG_DATA_HOME/vim/backup
set directory=$XDG_CACHE_HOME/vim/swap
set packpath^=$XDG_DATA_HOME/vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after
set runtimepath+=$XDG_DATA_HOME/vim
set undodir=$XDG_CACHE_HOME/vim/undo

if !has('nvim') " Neovim has its own special location
  set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
endif

call mkdir($XDG_CACHE_HOME."/vim/swap", "p")
call mkdir($XDG_DATA_HOME."/vim/spell", 'p')
call mkdir(&backupdir, 'p')
call mkdir(&undodir, 'p')

" }}}
```

# Step-by-step

If you haven't already set `XDG_CONFIG_HOME`, then you need to at least define
in your shell config:
```sh
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
```

To begin with, since version [7.3.1178](https://github.com/vim/vim/commit/6a459902592e2a4ba68),
Vim will search for `~/.vim/vimrc` if `~/.vimrc` is not found. So let's move
the file there. Yay, one less.

Let's move our `~/.vim` to `$XDG_CONFIG_HOME/vim`. Now we need to command Vim
to read config from this new location prior to `~/.vim`. Fortunately for us,
there is `VIMINIT` environmental variable.

```sh
export VIMINIT='set rtp^=$XDG_CONFIG_HOME/vim | let $MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc" | so $MYVIMRC'
```

If you wish to let Neovim configuration be still separated from Vim, then:

```sh
export VIMINIT='if !has("nvim") | set rtp^=$XDG_CONFIG_HOME/vim | let $MYVIMRC="$XDG_CONFIG_HOME/vim/vimrc" | so $MYVIMRC | endif'
```

We tell Vim to add our custom location as top runtime directory, then set `$MYVIMRC`
to one located in there, to prevent Vim from setting it to whatever it wants and
finally source our new _vimrc_. That's all configuring from the shell side.

Now to configuring in _vimrc_. First of all, let's define fallback locations in
case `XDG_*` variables are not set.
```vim
if empty($XDG_CACHE_HOME)  | let $XDG_CACHE_HOME  = $HOME."/.cache"       | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = $HOME."/.config"      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = $HOME."/.local/share" | endif
```

Now let's add remaining entries to `runtimepath`:
```vim
set runtimepath+=$XDG_CONFIG_HOME/vim/after
set runtimepath+=$XDG_DATA_HOME/vim
```

`$XDG_CONFIG_HOME/vim/after` is just equivalent of `~/.vim/after`, but `$XDG_DATA_HOME/vim`
is brand new - we will keep backups, plugins, spelling files etc. in there.
Let's set them too:
```vim

set backupdir=$XDG_DATA_HOME/vim/backup
set packpath^=$XDG_DATA_HOME/vim
```

`backupdir` for backups and `packpath` for plugins, but what about spellings?
Well, this one is more tricky, because it isn't controlled by option. Instead
it search for `spell` directory in runtime path. If none is found then it falls
back to `~/.vim/spell`. So let's create one at desired location ourselves!
```vim
call mkdir($XDG_DATA_HOME."/vim/spell", 'p')
```

So far so good. We are left with cache files.
```vim
set directory=$XDG_CACHE_HOME/vim/swap
set undodir=$XDG_CACHE_HOME/vim/undo

if !has('nvim') " Neovim has its own special location
  set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
endif
```

And for the final touch, let's create `backupdir`, `directory` and `undodir`,
because Vim doesn't do it by itself
```vim
call mkdir($XDG_CACHE_HOME."/vim/swap", "p")
call mkdir(&backupdir, 'p')
call mkdir(&undodir, 'p')
```

Congratulations! Now your Vim is configured with accordance to XDG Base Directory
specification.

### Special thanks to Tom Vincent, who wrote original [Vim respect XDG](https://tlvince.com/vim-respect-xdg) back in 2011!
